plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    //version
    def versionMajor = 1
    def versionMinor = 1
    def versionPatch = 3

    compileSdk 34
    buildToolsVersion = '35.0.0 rc1'
    ndkVersion '26.2.11394342'

    defaultConfig {
        applicationId 'com.jacktor.batterylab'
        namespace applicationId
        minSdkVersion 23
        targetSdkVersion 34
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "BUILD_DATE", "\"${getBuildDate()}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resourceConfigurations += ['en', 'in']

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            manifestPlaceholders += [appLabel: "@string/app_name"]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true

            versionNameSuffix ".${new Date().format('ddMMyyyy')}-dev"
            manifestPlaceholders += [appLabel: "@string/app_name_dev"]
            //resValue "bool", enabled_debug_options", "true"
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def appName = "Batttery Lab"
                outputFileName = "$appName v${variant.versionName} (Build " +
                        "${variant.versionCode}).apk"
            }
        }
    }

    bundle {
        language {
            enableSplit false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }

    buildFeatures {
        //dataBinding true
        viewBinding true
        buildConfig true
    }

    androidResources {
        ignoreAssetsPattern '*.md'
    }
}

static def getBuildDate() {
    return new Date().format('dd.MM.yyy HH:mm')
}

dependencies {
    /* Google  */
    implementation platform('com.google.firebase:firebase-bom:32.8.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.work:work-runtime-ktx:2.9.0'
    implementation 'com.android.billingclient:billing-ktx:6.2.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
    implementation 'com.google.android.gms:play-services-ads:23.0.0'
    implementation 'com.google.guava:guava:32.1.3-jre'

    /* Kotlin */
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.23'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'

    /* Other Java */
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'com.github.XomaDev:MIUI-autostart:v1.3'
    implementation 'com.github.topjohnwu.libsu:core:5.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    modules {
        module("com.google.guava:listenablefuture") {
            replacedBy("com.google.guava:guava", "listenablefuture is part of guava")
        }
    }
}